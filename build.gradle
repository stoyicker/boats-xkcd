import java.nio.file.Paths

apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    jcenter()
  }

  def androidPluginVersion = "2.0.0-alpha3"
  def frodoPluginVersion = "0.8.2"
  def versionsPluginVersion = "0.11.3"
  def dexcountPluginVersion = "0.3.1"
  def androidAptPluginVersion = "1.8"
  def pSyncPluginVersion = "1.1.5"
  def fileDownloadPluginVersion = "2.0.0"
  def jsonSchema2PojoPluginVersion = "0.4.16"

  def classpathDependencies = [androidPlugin        : "com.android.tools.build:gradle:${androidPluginVersion}",
                               frodoPlugin          : "com.fernandocejas.frodo:frodo-plugin:${frodoPluginVersion}",
                               gradleVersionsPlugin : "com.github.ben-manes:gradle-versions-plugin:${versionsPluginVersion}",
                               dexcountPlugin       : "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${dexcountPluginVersion}",
                               androidAptPlugin     : "com.neenbedankt.gradle.plugins:android-apt:${androidAptPluginVersion}",
                               pSync                : "gradle.plugin.com.flipboard:psync:${pSyncPluginVersion}",
                               fileDownloadPlugin   : "de.undercouch:gradle-download-task:${fileDownloadPluginVersion}",
                               jsonSchema2PojoPlugin: "org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:${jsonSchema2PojoPluginVersion}"]

  dependencies {
    classpath classpathDependencies.androidPlugin
    classpath classpathDependencies.frodoPlugin
    classpath classpathDependencies.gradleVersionsPlugin
    classpath classpathDependencies.dexcountPlugin
    classpath classpathDependencies.androidAptPlugin
    classpath classpathDependencies.pSync
    classpath classpathDependencies.fileDownloadPlugin
    classpath classpathDependencies.jsonSchema2PojoPlugin
  }
}

apply plugin: "com.github.ben-manes.versions"

//If this is modified, remember to update the script(s) accordingly
def staticAnalysisReportFolderTarget = "staticAnalysisReport"

allprojects {
  ext {
    //Application details
    androidVersionCode = 1
    androidVersionName = "0"
    androidTestInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"

    //Gradle wrapper version
    gradleWrapperVersion = '2.10'

    androidApplicationId = "com.jorge.boats"
    javaVersion = JavaVersion.VERSION_1_7

    staticAnalysisReportTarget = staticAnalysisReportFolderTarget
  }
}

task wrapper(type: Wrapper) {
  group "Build Setup"
  description "Creates the gradle wrapper."

  //noinspection GroovyAssignabilityCheck
  gradleVersion rootProject.ext.gradleWrapperVersion
}

task clean(type: Delete, overwrite: true) {
  group "Verification"
  description "Deletes the build directories of the whole project, the downloaded proguard files and the static analysis result."

  for (final Project x : rootProject.allprojects) {
    delete x.buildDir
    delete Paths.get(x.getProjectDir().absolutePath, staticAnalysisReportFolderTarget).
        toAbsolutePath().
        toString()
  }

  Arrays.asList(Paths.get("presentation", "proguard", "proguardFileContainer").
      toFile().
      listFiles()).
      stream().
      each({ f -> delete f })
}