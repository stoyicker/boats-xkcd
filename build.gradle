import java.nio.file.Paths

apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

buildscript {
  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    jcenter()
  }

  final def androidPluginVersion      = "2.3.3"
  final def dexCountPluginVersion     = "0.5.6"
  final def pSyncPluginVersion        = "1.1.5"
  final def versionsPlugin            = "0.13.0"

  final
  def classpathDependencies = [androidPlugin        : "com.android.tools.build:gradle:${androidPluginVersion}",
                               dexcountPlugin       : "com.getkeepsafe.dexcount:dexcount-gradle-plugin:${dexCountPluginVersion}",
                               pSync                : "gradle.plugin.com.flipboard:psync:${pSyncPluginVersion}",
                               versionsPlugin       : "com.github.ben-manes:gradle-versions-plugin:${versionsPlugin}"]

  dependencies {
    classpath classpathDependencies.androidPlugin
    classpath classpathDependencies.dexcountPlugin
    classpath classpathDependencies.pSync
    classpath classpathDependencies.versionsPlugin
  }
}

//If this is modified, remember to update the script(s) accordingly
final def staticAnalysisReportFolderTarget = project.rootDir.absolutePath + "/staticAnalysisReport"

allprojects {
  ext {
    //Application details
    androidVersionCode = 508
    androidVersionName = '1.20'
    androidTestInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"

    androidApplicationIdBase = "com.jorge.boats.xkcd%s"
    javaVersion = JavaVersion.VERSION_1_7

    staticAnalysisReportTarget = staticAnalysisReportFolderTarget
  }
}

task clean(type: Delete, overwrite: true) {
  group "Verification"
  description "Deletes the build directories of the whole project and the static analysis result."

  for (final Project each : rootProject.allprojects) {
    delete each.buildDir
    delete Paths.get(each.projectDir.absolutePath, staticAnalysisReportFolderTarget).
        toAbsolutePath().
        toString()
  }
}
